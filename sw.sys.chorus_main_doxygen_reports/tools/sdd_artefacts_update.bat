@echo off
rem -[Description]-----------------------------------------------------------------
rem run all the create_detail_design.bat and update all the xml files generated by it.
rem -[History]---------------------------------------------------------------------
rem  $Author: Wang Duo (uif54523) (uif54523) $: $Date: 2023/03/24
rem -------------------------------------------------------------------------------
@echo off
rem -[CONFIG]----------------------------------------------------------------------
SET TICKET=VWICAS23-116078
rem -------------------------------------------------------------------------------
cd /d %~dp0
SET OLD_DIR=%CD%
SET DATE_STR=%date%_%time%
rem replace [ ] and /
SET DATE_STR=%DATE_STR:-=_%
SET DATE_STR=%DATE_STR: =_%
SET DATE_STR=%DATE_STR:/=_%
SET DATE_STR=%DATE_STR:.=_%
SET DATE_STR=%DATE_STR:,=_%
SET DATE_STR=%DATE_STR::=_%


SET "REQUIRED_REPOS=sw.sys.chorus_main_doxygen_reports sw.sys.chorus_main_build sw.sys.chorus_main_doc sw.sys.chorus_main_tools"
REM SET "VARIANTS=Evo EvoF"
rem SET "VARIANTS=Evo"

cd ..\..\
SET ROOT_DIR=%CD%
rem now we are at the level of the parrent folder containing the repos
FOR %%T IN (%REQUIRED_REPOS%) DO (
	IF NOT EXIST %%T (
		git clone git@github-vni.geo.conti.de:bs-vw-meb-19-icas1-chorus/%%T.git

	) ELSE (
		cd %%T
		git reset --hard
		git pull
		cd ..
	)
)

IF EXIST .\temp\SDD (
	rd /s /q temp\SDD
)
mkdir .\temp\SDD

sw.sys.chorus_main_tools\Tools\python\2.7.9_2\python .\sw.sys.chorus_main_doxygen_reports\tools\python\sdd_trigger.bat.py .\sw.sys.chorus_main_doc .\temp\SDD\sdd_trigger.bat

cd .\temp\SDD
call sdd_trigger.bat
cd %ROOT_DIR%


if not defined DEBUG (
	rd /s /q temp
	)

cd sw.sys.chorus_main_doxygen_reports
set "XML_DIR=mc_sw"
git rm --cached mc_sw\*.xml

setlocal enabledelayedexpansion
for /r %XML_DIR% %%f in (*.xml) do (
	set "FILE_PATH=%%f"
	git add !FILE_PATH!
)
endlocal

git commit -m "%TICKET%/%USERNAME%: Automatic update of artefacts on %DATE_STR%"

git push origin master
cd %ROOT_DIR%

cd %OLD_DIR%

goto end

:error_handling

if defined ERROR_DESCRIPTION (
echo -[ERROR]-----------------------------------------------------------------------
	echo %ERROR_DESCRIPTION%
	exit /B
)
:end
echo -[DONE]------------------------------------------------------------------------

